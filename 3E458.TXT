.Object Name: user                                         Define: Models_User
*    Version: 0001
*
*===============================================================================
:INCLUDE,ECAB$,B,0002 .
@.
@. Entry point from User_Controller
@.
@0001:.
@.
@     if <action> eq 'create' . ; gto 0199 .
@.
@. ---- User Model Validation ---- Exists
@     ldv <validation>s7=failure .  Set flag to success will be set to failure if error
@     if <name> eq '' ldv <valmsg>s50='Name can'tic$'t be blank' rsr 0200 . ; .
@     if <email> eq '' ldv <valmsg>s50='Email can'tic$'t be blank' rsr 0200 . ; .
@     if <password> eq '' ldv <valmsg>s50='Password can'tic$'t be blank' rsr 0200 . ; .
@     if <passconfirm> eq '' \
        ldv <valmsg>s50='Confirmation password can'tic$'t be blank' rsr 0200 . ; .
@.
@. ---- User Model Validation ---- Check the length
@     def,p <sz>i3,<name> if <sz> gt 50 ldv <valmsg>s50='Name too long' rsr 0200 . ; .
@     if <password> gt '' . ; gto 0002 .
@     def,p <sz>i3,<password> if <sz> lt 6 \
        ldv <valmsg>s50='Password must be at least 6 characters' rsr 0200 . ; .
@     def,p <sz>i3,<password> if <sz> gt 40 \
        ldv <valmsg>s50='Password can'tic$'t be more than 40 chars' rsr 0200 . ; .
@0002:if <passconfirm> gt '' . ; gto 0003 .
@     def,p <sz>i3,<passconfirm> if <sz> lt 6 \
        ldv <valmsg>s50='Confirmation password must be at least 6 characters' rsr 0200 . ; .
@     def,p <sz>i3,<passconfirm> if <sz> gt 40 \
        ldv <valmsg>s50='Confirmation password can'tic$'t be more than 40 chars' rsr 0200 . ; .
@.
@. ---- User Model Validation ---- Email uniqueness
@0003:fdr,DB_Users,,,lin1 '' 'email' 	,<email> ldv <valmsg>s50='Email already exists' rsr 0200 .
@.
@. ---- Save the error(s) if any ----
@     if <flash> eq true ldv <validation>h7=failure rsr 0210 .  Save to St Mgmt
@     if <flash> eq false ldv <validation>h7=success .  Passed all edits
@     gto 0199 .  Exit
@.
@. ---- Find users by their id ----
@0020:.
@     fdr,DB_Users,,,0000 '' 'id' 	,<id> .
@     rln 'name','email' <name>s,<email>s .
@     gto 0199 .
@.
@. ---- Create a new user ----
@0060:.
@     dfu DB_Users .
@     lzr,DB_Users <tlns>i7,,<hdg>i7 .
@     fnd,DB_Users,<hdg>,lin1 'a@' 'id' 	,@ ,<nxtLn>i7 gto 0065 .
@     lln,DB_Users <lln>i7 .
@     ln+,DB_Users,<lln>,500 .
@     inc <lln> .
@     inc,<lln> <nxtLn> .
@.
@0065:rdl,DB_Users,2 86-12 <id>h .
@     ldv <date>h8=date11$,<time>h8=time$ .
@     ldv,l <id> .
@     wrl,DB_Users,<nxtLn>,y 'id','name','email','date','time' \
        	,<id>,<name>,<email>,<date>,<time> .
@     ldv,p <userid>h12=<id> .
@     inc <id> .
@     wrl,DB_Users,2,y 86-12 .,<id> .
@     cmu .
@     ldv <validation>h7=success .
@     ldv <valmsg>s50='Welcome to the Sample App!' rsr 0200 .
@     rsr 0210 . Save to St Mgmt
@     gto 0199 . Exit
@.
@.
@. ---- E S R back to the controller ----
@0199:.
@     esr .
@.
@.
@.
@. ---- F L A S H - V A L I D A T I O N  (Success | Failure) ----
@0200:.
@     ldv <flash>h4=true .  Set flag
@     if <hdgs> eq done,(0205) .
.Flash Messages    Flash Type: <validation>
*                                                                        .
*msgs                                                                    .
*========================================================================.
@0205:.
@     ldv <hdgs>h4=done .
	<valmsg>
@     esr .
@.
@. ---- Save the "flash" result to St. Mgmt ----
@0210:brk .
@     rnm -1 .
@     call,<GSvcHndCab>,<GSvcHndDrw>,16 001('flash',<sessionid>,'-1',\
        <GIceRepCab>,<msg>a3,<msgtxt>s256,<status>a1) .
@. ---- Save the server side ssflash to true to trigger message ----
@     call,<GSvcHndCab>,<GSvcHndDrw>,27 001* \
       ('ssflash','True',<*category>,<*service>,<sessionid>,<GIceRepCab>,\
       <msg>a3,<msgtxt>s256,<status>a1) .
@     esr .
